// Create shadow defined by Google's Material Design
import QtQuick 2.0
import QKit 1.0

Item {
    id : shadow

    property int depth : 1;
    property bool asynchronous: false

    // Set this property to true to use the shadow generated by shader
    property bool shader : false

    Drawable {
        id : drawable
        anchors.fill: parent
        anchors.leftMargin: -fillArea.x
        anchors.rightMargin: -fillArea.rightMargin
        anchors.topMargin: -fillArea.y
        anchors.bottomMargin: -fillArea.bottomMargin
    }

    Binding { target: drawable ; property: "source" ; value: Qt.resolvedUrl("drawable/ArtShadowDepth1.qml");when: !shader && depth == 1}
    Binding { target: drawable ; property: "source" ; value: Qt.resolvedUrl("drawable/ArtShadowDepth2.qml");when: !shader && depth == 2}
    Binding { target: drawable ; property: "source" ; value: Qt.resolvedUrl("drawable/ArtShadowDepth3.qml");when: !shader && depth == 3}
    Binding { target: drawable ; property: "source" ; value: Qt.resolvedUrl("drawable/ArtShadowDepth4.qml");when: !shader && depth == 4}
    Binding { target: drawable ; property: "source" ; value: Qt.resolvedUrl("drawable/ArtShadowDepth5.qml");when: !shader && depth == 5}


    Loader {
        id : bottomShadow
        asynchronous: shadow.asynchronous
        anchors.fill: parent
        active: shader
        property string color : "#000000"
        property int verticalOffset: 0
        property int horizontalOffset: 0
        property real blur: 0

        sourceComponent: Shadow {
                color: bottomShadow.color
                verticalOffset: bottomShadow.verticalOffset
                horizontalOffset: bottomShadow.horizontalOffset
                blur: bottomShadow.blur
            }
    }

    Loader {
        id : topShadow
        asynchronous: shadow.asynchronous
        active: shader
        anchors.fill: parent
        property string color : "#000000"
        property int verticalOffset: 0
        property int horizontalOffset: 0
        property real blur: 0

        sourceComponent: Shadow {
                color: topShadow.color
                verticalOffset: topShadow.verticalOffset
                horizontalOffset: topShadow.horizontalOffset
                blur: topShadow.blur
            }
    }

    // Reference: http://www.google.com/design/spec/layout/layout-principles.html#layout-principles-dimensionality

    states: [
        State {
            name: "D1"
            when : depth === 1

            PropertyChanges {
                target: topShadow
                verticalOffset : K.dp(1)
                horizontalOffset: 0
                opacity: 0.12
                blur : K.dp(1.5)
            }

            PropertyChanges {
                target: bottomShadow
                verticalOffset : K.dp(1)
                horizontalOffset: 0
                blur : K.dp(1)
                opacity: 0.24
            }

        },
        State {
            name: "D2"
            when : depth === 2

            PropertyChanges {
                target: topShadow
                verticalOffset : K.dp(3)
                horizontalOffset: 0
                blur : K.dp(3)
                opacity: 0.16
            }

            PropertyChanges {
                target: bottomShadow
                verticalOffset : K.dp(3)
                horizontalOffset: 0
                blur : K.dp(3)
                opacity: 0.23
            }
        },
        State {
            name: "D3"
            when : depth === 3

            PropertyChanges {
                target: topShadow
                verticalOffset : K.dp(10)
                horizontalOffset: 0
                blur : K.dp(10)
                opacity: 0.19
            }

            PropertyChanges {
                target: bottomShadow
                verticalOffset : K.dp(6)
                horizontalOffset: 0
                blur : K.dp(3)
                opacity: 0.23
            }
        },

        State {
            name: "D4"
            when : depth === 4

            PropertyChanges {
                target: topShadow
                verticalOffset : K.dp(14)
                horizontalOffset: 0
                blur : K.dp(14)
                opacity: 0.25
            }

            PropertyChanges {
                target: bottomShadow
                verticalOffset : K.dp(10)
                horizontalOffset: 0
                blur : K.dp(5)
                opacity: 0.22
            }
        },
        State {
            name: "D5"
            when : depth === 5

            PropertyChanges {
                target: topShadow
                verticalOffset : K.dp(19)
                horizontalOffset: 0
                blur : K.dp(19)
                opacity: 0.30
            }

            PropertyChanges {
                target: bottomShadow
                verticalOffset : K.dp(15)
                horizontalOffset: 0
                blur : K.dp(6)
                opacity: 0.22

            }
        }
    ]
}
